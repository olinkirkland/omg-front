<?xml version="1.0"?>
<s:Panel xmlns:fx="http://ns.adobe.com/mxml/2009"
         creationComplete="onCreationComplete(event)"
         xmlns:s="library://ns.adobe.com/flex/spark"
         width="100%"
         height="100%"
         skinClass="skins.PanelSkin">

    <fx:Script>
        <![CDATA[
        import mx.events.FlexEvent;

        import pages.Page;
        import pages.PageType;
        import pages.ProfilePage;

        import pages.SplashPage;

        import signal.SignalEvent;
        import signal.SignalManager;
        import signal.SignalType;

        private var signalManager:SignalManager;
        private var page:Page;

        private function onCreationComplete(event:FlexEvent):void
        {
            // Signal Manager
            signalManager = SignalManager.getInstance();
            signalManager.addEventListener(SignalEvent.SIGNAL, handleSignal);

            // Force Resize
            invalidateSize();
            invalidateDisplayList();
            invalidateParentSizeAndDisplayList();
        }

        public function handleSignal(signalEvent:SignalEvent):void
        {
            // Handle global Signals
            if (signalEvent.action == SignalType.LOAD_PAGE)
                loadPage(signalEvent.payload.type, signalEvent.payload);
        }

        public function loadPage(type:String, payload:Object = null):void
        {
            // Unload current page
            if (page)
            {
                (page as Page).dispose();
                removeElement(page);
                page = null;
            }

            // Load new page
            switch (type)
            {
                case PageType.SPLASH:
                    // Load the Splash page by default
                    page = new SplashPage();
                    break;
                case PageType.PROFILE:
                    page = new ProfilePage();
                    break;
            }

            callLater(function ():void
            {
                addElement(page);
            });
        }
        ]]>
    </fx:Script>

</s:Panel>